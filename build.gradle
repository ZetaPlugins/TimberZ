/*
 * Copyright (C) 2025 ZetaPlugins
 *
 * This file is part of the Zetaplugins Project.
 *
 * This plugin is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 *
 * This plugin is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this plugin. If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id("com.gradleup.shadow") version "8.3.0"
}

group = 'com.zetaplugins'
version = '1.3.1'

repositories {
    mavenCentral()
    maven { name = "papermc-repo"; url = "https://repo.papermc.io/repository/maven-public/" }
    maven { name = "sonatype"; url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url = 'https://repo.extendedclip.com/releases/' }
    maven { url = "https://maven.zetaplugins.com/" }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    filteringCharset 'UTF-8'
    filesMatching("**/paper-plugin.yml") {
        expand version: project.version
    }
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
        resources.srcDirs = ['src/main/resources']
    }
    mc1214 {
        java.srcDirs = ['src/mc1214/java']
        resources.srcDirs = ['src/mc1214/resources']
    }
    mc1219 {
        java.srcDirs = ['src/mc1219/java']
        resources.srcDirs = ['src/mc1219/resources']
    }
}

dependencies {
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.13'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'dev.aurelium:auraskills-api-bukkit:2.2.4'
    implementation "com.fasterxml.jackson.core:jackson-databind:2.16.1"

    shadow "com.zetaplugins:zetacore:1.7.1"

    compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"

    mc1214CompileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"
    mc1219CompileOnly "io.papermc.paper:paper-api:1.21.9-R0.1-SNAPSHOT"

    mc1214Implementation sourceSets.main.output
    mc1219Implementation sourceSets.main.output
}

tasks.register('buildMC1214', Jar) {
    archiveClassifier = '1.21.4'
    from sourceSets.mc1214.output
    from sourceSets.main.output

    from(configurations.shadow)
}

tasks.register('buildMC1219', Jar) {
    archiveClassifier = '1.21.9'
    from sourceSets.mc1219.output
    from sourceSets.main.output

    from(configurations.shadow)
}

build.dependsOn(buildMC1214, buildMC1219)

tasks.named("jar") {
    enabled = false
}